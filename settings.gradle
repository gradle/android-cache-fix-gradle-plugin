plugins {
    id 'com.gradle.enterprise' version '3.10.2'
    id 'com.gradle.common-custom-user-data-gradle-plugin' version '1.7.2'
}

def isCI = System.getenv('GITHUB_ACTIONS') != null

gradleEnterprise {
    server = "https://ge.solutions-team.gradle.com"
    buildScan {
        capture { taskInputFiles = true }
        uploadInBackground = !isCI
        publishAlways()
        publishIfAuthenticated()
        obfuscation {
            ipAddresses { addresses -> addresses.collect { address -> "0.0.0.0"} }
        }
    }
}

buildCache {
    local {
        enabled = true
    }

    remote(HttpBuildCache) {
        url = 'https://ge.solutions-team.gradle.com/cache/'
        allowUntrustedServer = false
        credentials { creds ->
            creds.username = System.getenv('GRADLE_ENTERPRISE_CACHE_USERNAME')
            creds.password = System.getenv('GRADLE_ENTERPRISE_CACHE_PASSWORD')
        }
        enabled = true
        push = isCI
    }
}

rootProject.name = 'android-cache-fix-gradle-plugin'

